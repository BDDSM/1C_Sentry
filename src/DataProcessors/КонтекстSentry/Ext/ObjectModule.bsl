#Область Интерфейс	

// Инициализация базовых параметров
//
//
Процедура Инициализировать() Экспорт

    Тело = Новый Соответствие;

КонецПроцедуры // Инициализировать()

Функция ИмяКонтекста(ИмяКонтекста) Экспорт

	Тело.Вставить("name", ИмяКонтекста);
	
	Возврат ЭтотОбъект;
	
КонецФункции

#Область Тип_контекста
Функция ТипКонтекста(ТипКонтекста) Экспорт

	Тело.Вставить("type", ТипКонтекста);

	Возврат ЭтотОбъект;
	
КонецФункции

Функция ЭтоОперационнаяСистема() Экспорт

	Возврат ТипКонтекста("os");
	
КонецФункции

Функция ЭтоПлатформа1с() Экспорт

	Возврат ТипКонтекста("app").ИмяКонтекста("1с");

КонецФункции	

Функция ЭтоКонфигурация1с() Экспорт
	
	Возврат ТипКонтекста("runtime");
	
КонецФункции

#КонецОбласти

Функция ПолноеИмяКонтекста(ПолноеИмя) Экспорт
	
	Тело.Вставить("raw_description", ПолноеИмя);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция Версия(Версия) Экспорт
	
	Тело.Вставить("version", Версия);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция ДополнительныйПараметр(ЗНАЧ ИмяПараметра, ЗначениеПараметра) Экспорт
	
	Тело.Вставить(ИмяПараметра, ЗначениеПараметра);
	
	Возврат ЭтотОбъект;
	
КонецФункции


#КонецОбласти

#Область Терминальные_операции


Функция ПолучитьОписание() Экспорт

	Если Тело["type"] = Неопределено Тогда

		Тело.Вставить("type", "runtime");
	
	КонецЕсли;		
		
	
	Возврат Тело;
	
КонецФункции

#КонецОбласти
